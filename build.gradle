import groovy.json.JsonOutput
import groovy.json.JsonSlurper

/**
 * Get all the Neo4j releases
 * The source of truth is https://api.github.com/repos/neo4j/neo4j/releases
 * We don't consider all the tags with "prerelease" attribute to true
 *
 * @return
 */
def collectNeo4jReleases() {
    def slurper = new JsonSlurper()

    // get from tags or releases?
    // https://api.github.com/repos/neo4j/neo4j/releases
    def releases = slurper.parseText(new URL("http://search.maven.org/solrsearch/select?q=g:%22org.neo4j%22+AND+a:%22neo4j%22&core=gav&rows=500&wt=json").getText())
    releases = releases.response.docs

    def actualReleases = []
    for (release in releases) {
        actualReleases.add(release.v)
    }

    // We sort considering that X.Y.Z-SOMETHING should be considered lower than X.Y.Z --> it's not a lexicographical ordering
    return actualReleases
            .findAll { it >= "3.0" }
            .sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            .reverse()
}

def orderVersion(str) {
  return str.split("[.-]").collect{ it =~ "^\\d+\$" ? it.padLeft(4,"0") : it.replace("beta","mbeta").replace("BETA","MBETA") }.join(".") + "/"
}
/**
 * Coupling Neo4j with Neo4j Graph Algorithms
 *
 * There are two heuristic in place:
 *
 * - latest version of Neo4j with latest version of Graph Algos
 * - if there is no available version of Graph Algos for a given NEO4j release than take the prior available version of Graph Algos
 */
task versions {
    def neo4jReleases = collectNeo4jReleases()

    def slurper = new JsonSlurper()

    // get tags
    def pattern = ~/<neo4j.version>(.+?)<\/neo4j.version>/
    def releases = slurper.parseText(new URL("https://api.github.com/repos/neo4j-contrib/neo4j-graph-algorithms/releases?page=1&per_page=500").getText())
    def versions = []
    def neo4jToAlgos = [:]
    def latestAlgos, oldestAlgos
    for (release in releases) {
        if (release.draft) continue;
        def buildText = new URL("https://raw.githubusercontent.com/neo4j-contrib/neo4j-graph-algorithms/" + release.tag_name + "/pom.xml").getText()
        def matcher = pattern.matcher(buildText)
        for (def i = 0; i < matcher.getCount(); i++) {
            def neo4jVersion = matcher[i][1]

            println "neo " + neo4jVersion + ", algos " + release.tag_name 
            if (!neo4jToAlgos.containsKey(neo4jVersion)) {
                neo4jToAlgos[neo4jVersion] = release.tag_name
            }

            if (latestAlgos == null) {
                latestAlgos = release.tag_name
            }

            oldestAlgos = release.tag_name
        }
    }

    println "LATEST ALGOS IS " + latestAlgos + " WHILE OLDEST IS " + oldestAlgos
    println(neo4jToAlgos)
    println(neo4jReleases)
    for (neo4jRelease in neo4jReleases) {
        def prefix = neo4jRelease.substring(0,Math.min(4,neo4jRelease.length()))
        if (prefix < "3.0") continue;

        def algosVersion = neo4jToAlgos[neo4jRelease]

        // If there is no ALGOS release for a Neo4j release we have to guess which one to associate
        if (algosVersion == null) {
            def matchingNeo4jVersions = neo4jToAlgos.keySet().findAll{ it.substring(0,4) == prefix }.sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            def matchingAlgosVersions = neo4jToAlgos.subMap(matchingNeo4jVersions).values().sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            matchingNeo4jVersions = matchingNeo4jVersions.findAll{ orderVersion(it) < orderVersion(neo4jRelease) }
            def matchingNeo4jVersion = matchingNeo4jVersions.isEmpty() ? null : matchingNeo4jVersions[-1]
            def firstAlgosVersion = matchingAlgosVersions.isEmpty() ? null : matchingAlgosVersions[0]
            algosVersion = neo4jToAlgos[matchingNeo4jVersion]==null ? firstAlgosVersion : neo4jToAlgos[matchingNeo4jVersion]
        }
        if (algosVersion != null) {
            def version = [ neo4j : neo4jRelease, version : algosVersion, 
                            url : "http://github.com/neo4j-contrib/neo4j-graph-algorithms/releases/" + algosVersion,
                            jar: "https://github.com/neo4j-contrib/neo4j-graph-algorithms/releases/download/${algosVersion}/graph-algorithms-algo-${algosVersion}.jar" ]
            versions.add(version)
        }
    }

    def versionsFile = new File("versions.json")

    versionsFile.write(JsonOutput.prettyPrint(JsonOutput.toJson(versions)))
}
